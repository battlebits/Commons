plugins {
    id "io.freefair.lombok" version "3.8.1"
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'java'
    id 'maven-publish'
}

group 'br.com.battlebits'
version '1.0'

sourceCompatibility = 1.8

apply plugin: 'java'
jar.enabled = false

tasks.build.dependsOn tasks.shadowJar

repositories {
    maven { url = uri("https://hub.spigotmc.org/nexus/content/groups/public") }
    maven { url "http://repo.dmulloy2.net/nexus/repository/public/" }
}

dependencies {
    compile group: 'org.spigotmc', name: 'spigot-api', version: '1.12.2-R0.1-SNAPSHOT'
    compile group: 'org.javassist', name: 'javassist', version: '3.15.0-GA'
    compile 'com.comphenix.protocol:ProtocolLib:4.4.0'
    compile project(':commons-core')
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

processResources {
    if (System.getenv('BUILD_NUMBER')) {
        version = version + '.' + System.getenv('BUILD_NUMBER')
    }
    if (System.getenv('GIT_BRANCH') && System.getenv('GIT_BRANCH').contains('develop')) {
        version = version + '-SNAPSHOT'
    }
    filesMatching('plugin.yml') {
        expand(["fullVersion":version])
    }
}

shadowJar {
    dependencies {
        include(project(':commons-core'))
    }
    archiveClassifier = null
//    archiveName = "${baseName}.${extension}"
}

publishing {
    if(!project.hasProperty("nexusUser") || !project.hasProperty("nexusPassword"))
        return;
    def nexusUser = "${nexusUser}"
    def nexusPassword = "${nexusPassword}"
    publications {
        mavenBukkit(MavenPublication) {
            from components.java
            afterEvaluate {
                artifactId = 'commons-bukkit'
            }
        }
    }
    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ? 'https://repo.battlebits.com.br/repository/maven-snapshots/':
                    'https://repo.battlebits.com.br/repository/maven-releases/'
            credentials {
                username nexusUser
                password nexusPassword
            }
        }
    }
}